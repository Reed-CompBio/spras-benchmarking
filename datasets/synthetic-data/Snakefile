pathways = ["Apoptosis_signaling", "B_cell_activation",
            "Beta3_adrenergic_rec", "Cadherin_signaling",
            "Hedgehog_signaling", "Insulin_IGF",
            "Interleukin_signaling", "Notch_signaling",
            "PDGF_signaling", "Ras", "T_cell_activation",
            "Toll_signaling", "Wnt_signaling", "p38_MAPK",
            "Nicotinic_acetylchol", "Fas_signaling",
            "FGF_signaling", "Interferon_gamma_signaling",
            "JAK_STAT_signaling", "VEGF_signaling"]
# TODO: deduplicate this from thresholding scripts by passing it in?
thresholds = [1, 100, 200, 300, 400, 500, 600, 700, 800, 900]

rule all:
    input:
        "intermediate/9606.protein.links.full.v12.0.txt",
        expand([
            "processed/{pathway}/{pathway}_node_prizes.txt",
            "processed/{pathway}/{pathway}_gs_edges.txt",
            "processed/{pathway}/{pathway}_gs_nodes.txt"
        ], pathway=pathways),
        expand(
            "processed/interactomes/uniprot-combined-threshold-interactomes/uniprot_combined_interactome_{threshold}.txt",
            threshold=thresholds)

rule combine_interactomes:
    input:
        expand("processed/{pathway}/{pathway}_gs_edges.txt", pathway=pathways),
        expand(
            "processed/interactomes/uniprot-threshold-interactomes/uniprot_human_interactome_{threshold}.txt",
            threshold=thresholds)
    output:
        "processed/interactomes/uniprot-combined-threshold-interactomes/overlap_info.csv",
        "processed/interactomes/uniprot-combined-threshold-interactomes/overlap_combined_info.csv",
        expand(
            "processed/interactomes/uniprot-combined-threshold-interactomes/uniprot_combined_interactome_{threshold}.txt",
            threshold=thresholds)
    shell:
        "uv run src/combine.py"

rule threshold_interactomes:
    input:
        "intermediate/9606.protein.links.full.v12.0.txt",
        expand([
            "processed/{pathway}/{pathway}_node_prizes.txt",
            "processed/{pathway}/{pathway}_gs_edges.txt",
            "processed/{pathway}/{pathway}_gs_nodes.txt"
        ], pathway=pathways)
    output:
        "processed/interactomes/uniprot-threshold-interactomes/proteins_missing_aliases.csv",
        "processed/interactomes/uniprot-threshold-interactomes/removed_edges.txt",
        expand(
            "processed/interactomes/uniprot-threshold-interactomes/uniprot_human_interactome_{threshold}.txt",
            threshold=thresholds)
    shell:
        "uv run src/threshold_interactomes.py"

rule process_panther_pathway:
    input: "raw/pathway-data/{pathway}.txt"
    output:
        [
            "intermediate/{pathway}/EDGES.txt",
            "intermediate/{pathway}/NODES.txt",
            "intermediate/{pathway}/TARGETS.txt",
            "intermediate/{pathway}/SOURCES.txt",
            "intermediate/{pathway}/PRIZES.txt"
        ]
    shell:
        "uv run src/process_panther_pathway.py {wildcards.pathway}"

rule make_spras_compatible:
    input:
        [
            "intermediate/{pathway}/EDGES.txt",
            "intermediate/{pathway}/NODES.txt",
            "intermediate/{pathway}/TARGETS.txt",
            "intermediate/{pathway}/SOURCES.txt",
            "intermediate/{pathway}/PRIZES.txt"
        ]
    output:
        [
            "processed/{pathway}/{pathway}_node_prizes.txt",
            "processed/{pathway}/{pathway}_gs_edges.txt",
            "processed/{pathway}/{pathway}_gs_nodes.txt"
        ]
    shell:
        "uv run src/SPRAS_compatible_files.py {wildcards.pathway}"

rule download_interactome:
    output:
        "intermediate/9606.protein.links.full.v12.0.txt.gz"
    shell:
        "uv run src/download_interactome.py"

rule unpack_interactome:
    input:
        "intermediate/9606.protein.links.full.v12.0.txt.gz"
    output:
        "intermediate/9606.protein.links.full.v12.0.txt"
    shell:
        "gunzip intermediate/9606.protein.links.full.v12.0.txt.gz"
